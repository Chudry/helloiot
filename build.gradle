
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.3.4'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'org.javafxports.jfxmobile'

group = 'com.github.adrianromero'
version = '1.0.10'

mainClassName = 'com.adr.helloiot.MainAppClient'
ext.mainClass = 'com.adr.helloiot.MainAppClient'

description = """"""

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://repo.eclipse.org/content/repositories/paho-releases/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile group: 'com.github.adrianromero', name: 'fonticon', version:'1.2.2'
    compile group: 'com.github.adrianromero', name: 'hellocommon', version:'1.2.9'
    compile group: 'com.github.adrianromero', name: 'textflowbuilder', version: '1.0.3'
    compile group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version:'1.1.1'
    compile group: 'com.jidesoft', name: 'jidefx-common', version:'0.9.1'
    compile group: 'eu.hansolo', name: 'Medusa', version:'7.7'
    compile group: 'commons-io', name: 'commons-io', version:'2.5'
    compile group: 'org.mapdb', name: 'mapdb', version: '3.0.3'
    compile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.2.0'
}

applicationDistribution.from("${rootProject.projectDir}") {
    include "LICENSE", \
        "helloiot.properties", \
        "application*", \
        "mosquitto*"
    into "bin"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'GNU General Public License, Version 3, 29 June 2007'
          url 'http://www.gnu.org/licenses/gpl-3.0.txt'
          distribution 'repo'
        }
      }
    }
  }
}
jfxmobile {
    embedded {
        remotePlatforms {
            raspberry {
                host = 'raspberrypi.local'
                username = 'pi'
                password = 'raspberry'
                workingDir = '/home/pi/helloiot'
                jreLocation = '/usr/lib/jvm/jdk-8-oracle-arm32-vfp-hflt'
                execPrefix = 'sudo'
            }
        }
    }    
}
