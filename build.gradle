//    HelloIoT is a dashboard creator for MQTT
//    Copyright (C) 2017-2018 Adri√°n Romero Corchado.
//
//    This file is part of HelloIot.
//
//    HelloIot is free software: you can redistribute it and/or modify
//    it under the terms of the GNU General Public License as published by
//    the Free Software Foundation, either version 3 of the License, or
//    (at your option) any later version.
//
//    HelloIot is distributed in the hope that it will be useful,
//    but WITHOUT ANY WARRANTY; without even the implied warranty of
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//    GNU General Public License for more details.
//
//    You should have received a copy of the GNU General Public License
//    along with HelloIot.  If not, see <http://www.gnu.org/licenses/>.
//
buildscript {
    repositories {       
        jcenter()
        // google()
        // maven { url 'http://nexus.gluonhq.com/nexus/content/repositories/releases/' }
    }
    dependencies {
        classpath 'org.javafxports:jfxmobile-plugin:1.3.10'
        // classpath 'org.javafxports:jfxmobile-plugin:2.0.19'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'org.javafxports.jfxmobile'

group = 'com.github.adrianromero'
version = '1.0.29'

mainClassName = 'com.adr.helloiot.MainAppClient'
ext.mainClass = 'com.adr.helloiot.MainAppClient'

description = """"""

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://repo.eclipse.org/content/repositories/paho-releases/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile group: 'com.github.adrianromero', name: 'fonticon', version:'1.2.4'
    compile group: 'com.github.adrianromero', name: 'hellocommon', version:'1.2.18'
    compile group: 'com.github.adrianromero', name: 'textflowbuilder', version: '1.0.3'
    compile group: 'org.eclipse.paho', name: 'org.eclipse.paho.client.mqttv3', version:'1.2.0'
    compile group: 'com.jidesoft', name: 'jidefx-common', version:'0.9.1'
    compile group: 'eu.hansolo', name: 'Medusa', version:'7.9'
    compile group: 'commons-io', name: 'commons-io', version:'2.5'
    compile group: 'com.google.guava', name: 'guava', version: '20.0'
    compile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.2.0'
    compile group: 'javax.jmdns', name: 'jmdns', version: '3.2.2'
    compile group: 'org.mozilla', name: 'rhino', version: '1.7.7.1'  
    
    // Tradfri - Coap
    compile group: 'org.eclipse.californium', name: 'californium-core', version: '2.0.0-M8'
    compile group: 'org.eclipse.californium', name: 'scandium', version: '2.0.0-M8'
    compile group: 'org.eclipse.californium', name: 'element-connector', version: '2.0.0-M8'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.6.2'    
}

applicationDistribution.from("${rootProject.projectDir}") {
    include "LICENSE", \
        "README.md",\
        "helloiot_template.properties"
    into ""
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'GNU General Public License, Version 3, 29 June 2007'
          url 'http://www.gnu.org/licenses/gpl-3.0.txt'
          distribution 'repo'
        }
      }
    }
  }
}

jfxmobile {
    android {
        compileSdkVersion = '24'
        minSdkVersion = '24'        
        manifest = 'src/android/AndroidManifest.xml'
        packagingOptions {
            exclude 'epl-v10.html'
            exclude 'edl-v10.html'
            exclude 'notice.html'
            pickFirst 'META-INF/INDEX.LIST'
        }        
        signingConfig {
            // keytool -genkey -v -keystore my-release-key.jks -keyalg RSA -keysize 2048 -validity 10000 -alias app
            storeFile file('my-release-key.jks')
            storePassword '123456'
            keyAlias 'app'
            keyPassword '123456'
        }
        
    }    
    embedded {
        remotePlatforms {
            raspberry {
                host = 'raspberrypi.local'
                username = 'pi'
                password = 'raspberry'
                workingDir = '/home/pi/helloiot'
                jreLocation = '/usr/lib/jvm/jdk-8-oracle-arm32-vfp-hflt'
                execPrefix = 'sudo'
            }
        }
    }    
}
